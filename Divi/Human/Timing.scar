{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                              Official SCAR Include
                                 Timing Routines
--------------------------------------------------------------------------------
 * procedure WaitRR(Min, Max);
     By: Wanted
 * function  WaitFunc(Func: function: Boolean; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
     By: Wanted
 * function  WaitFuncEx(Func: string; Parameters: TVariantArray; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
     By: Wanted     
 * function WaitFindColorEx(var TPA: TPointArray; Color: Integer; B: TBox; DesiredCount: Integer; GreaterThan: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
     By: Wanted
 * function WaitFindColorTol(var P: TPoint; Color: Integer; B: TBox; Tol: Integer; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
     By: Wanted
 * function WaitFindColorTolEx(var TPA: TPointArray; Color: Integer; B: TBox; Tol, DesiredCount: Integer; GreaterThan: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
     By: Wanted
 * procedure DoNothing;
     By: Wanted
 * function  TheTime: string;
     By: Timer
 * function  TheDate(Format: Integer): string;
     By: Timer
 * function  TheMonth: string;
     By: Timer
 * function  MSToTime(MS: LongInt): string;
     By: Timer
 * function MSToTimeEx(MS: LongInt): string;
     By: Wanted    
 * function STimeRunning: string;
     By: Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
procedure WaitRR(Min, Max: LongInt);
Contributors: Wanted, Freddy.
Description: Waits/sleeps a random amount of time.
Date Created: August 10th, 2011. By Wanted
Last Modified: July 20th, 2012. By Freddy
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

procedure WaitRR(Min, Max: LongInt);
begin
  Wait(RR(Min, Max));
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function WaitFunc(Func: function: Boolean; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
Contributors: Wanted
Description: Repeats a function until DesiredResult.
Date Created: August 10th, 2011. By Wanted
Last Modified: September 26th, 2011. By Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function WaitFunc(Func: function: Boolean; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
var
  T: LongInt;
begin
  Result := True;
  T := GetSystemTime + RR(MinTotalWait, MaxTotalWait);
  while (GetSystemTime < T) do
    if (Func() = DesiredResult) then
      Exit
    else
      WaitRR(MinWait, MaxWait);
  Result := False;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function WaitFuncEx(Func: string; Parameters: TVariantArray; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
Contributors: Wanted
Description: Repeats a function until DesiredResult.
Date Created: September 26th, 2011. By Wanted
Last Modified: September 26th, 2011. By Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function WaitFuncEx(Func: string; Parameters: TVariantArray; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
var
  T: LongInt;
begin
  Result := True;
  T := GetSystemTime + RR(MinTotalWait, MaxTotalWait);
  while (GetSystemTime < T) do
    if (CallProc(Func, Parameters) = DesiredResult) then
      Exit
    else
      WaitRR(MinWait, MaxWait);
  Result := False;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function WaitFindColorEx(var TPA: TPointArray; Color: Integer; B: TBox; DesiredCount: Integer; GreaterThan: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
Contributors: Wanted, Freddy.
Description: Repeats FindColorEx until greater or less than Count.
Date Created: March 9th, 2013. By Wanted
Last Modified: March 9th, 2013. By Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function WaitFindColorEx(var TPA: TPointArray; Color: Integer; B: TBox; DesiredCount: Integer; GreaterThan: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
var
  T, L: LongInt;
begin
  Result := True;
  T := GetSystemTime + RR(MinTotalWait, MaxTotalWait);
  while (GetSystemTime < T) do
  begin
    FindColorExP(TPA, Color, B);
    L := Length(TPA);
    if (GreaterThan) then
    begin
      if (L > DesiredCount) then
        Exit;
    end
    else
      if (L < DesiredCount) then
        Exit;
    WaitRR(MinWait, MaxWait);
  end;
  Result := False;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function WaitFindColorTol(var P: TPoint; Color: Integer; B: TBox; Tol: Integer; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
Contributors: Wanted, Freddy.
Description: Repeats FindColorTol until DesiredResult.
Date Created: September 26th, 2011. By Wanted
Last Modified: July 20th, 2012. By Freddy
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function WaitFindColorTol(var P: TPoint; Color: Integer; B: TBox; Tol: Integer; DesiredResult: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
var
  T: LongInt;
begin
  Result := True;
  T := GetSystemTime + RR(MinTotalWait, MaxTotalWait);
  while (GetSystemTime < T) do
    if (FindColorTolP(P, Color, B, Tol) = DesiredResult) then
      Exit
    else
      WaitRR(MinWait, MaxWait);
  Result := False;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function WaitFindColorTolEx(var TPA: TPointArray; Color: Integer; B: TBox; Tol, DesiredCount: Integer; GreaterThan: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
Contributors: Wanted, Freddy.
Description: Repeats FindColorTolEx until greater or less than Count.
Date Created: October 28th, 2011. By Wanted
Last Modified: July 20th, 2012. By Freddy
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function WaitFindColorTolEx(var TPA: TPointArray; Color: Integer; B: TBox; Tol, DesiredCount: Integer; GreaterThan: Boolean; MinWait, MaxWait, MinTotalWait, MaxTotalWait: LongInt): Boolean;
var
  T, L: LongInt;
begin
  Result := True;
  T := GetSystemTime + RR(MinTotalWait, MaxTotalWait);
  while (GetSystemTime < T) do
  begin
    FindColorTolExP(TPA, Color, B, Tol);
    L := Length(TPA);
    if (GreaterThan) then
    begin
      if (L > DesiredCount) then
        Exit;
    end
    else
      if (L < DesiredCount) then
        Exit;
    WaitRR(MinWait, MaxWait);
  end;
  Result := False;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
procedure DoNothing;
Contributors: Wanted
Description: For use with while (this) do DoNothing
Date Created: August 10th, 2011. By Wanted
Last Modified: August 10th, 2011. By Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

procedure DoNothing;
begin
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function TheTime: string;
By: Timer
Description: Gets the Time.
Date Created: August 7th, 2011. By Timer
Last Modified: August 7th, 2011. By Timer
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function TheTime: string;
var
  Hour, Minz, Sec, MSec: Word;
  Padding: string;
begin
  DecodeTime(Now, Hour, Minz, Sec, MSec);
  Padding := 'AM';
  if (Hour > 12) then
  begin
    Hour := Hour - 12;
    Padding := 'PM';
  end else
    if (Hour = 12) then
      Padding := 'PM';
  Result := IntToStr(Hour) + ':' + IntToStr(Minz) + ':' + IntToStr(Sec) + ' ' + Padding;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function TheDate(Format: Integer): string;
By: Timer, Freddy.
Description: Gets the Date. Format of 0 = Month Day, Year
                            Format of 1 = Month-Day-Year abreviated 
Date Created: August 7th, 2011. By Timer
Last Modified: October 14th, 2012. By LordJashin
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function TheDate(Format: Integer): string;
var
  Year, Month, Day: Word;
  Months, suffixs: TStrArray;
  suffix: string;
begin
  DecodeDate(Date, Year, Month, Day);
  Months := ['January', 'February', 'March', 'April', 'May', 'June', 'July',
            'August', 'September', 'October', 'November', 'December'];
  suffixs := ['st', 'nd', 'rd'];
  if (InRange(Day mod 10, 1, 3)) and (not InRange(Day, 11, 13)) then
    suffix := suffixs[(Day mod 10) - 1]
  else
    suffix := 'th';                                                 
  case Format of
    0 : Result := Months[Month - 1] + ' ' + IntToStr(Day) + suffix + ', ' + IntToStr(Year);
    1 : Result := IntToStr(Month) + '-' + IntToStr(Day) + '-' + IntToStr(Year);
  end;
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function TheMonth: string;
By: Timer, Freddy.
Description: Gets the Month.
Date Created: August 7th, 2011. By Timer
Last Modified: July 20th, 2012. By Freddy
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function TheMonth: string;
var
  Year, Month, Day: Word;
  Months: TStrArray;
begin
  DecodeDate(Date, Year, Month, Day);
  Months := ['January', 'February', 'March', 'April', 'May', 'June', 'July',
            'August', 'September', 'October', 'November', 'December'];
  Result := Months[Month - 1];
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function MSToTime(MS: LongInt): string;
By: Timer, Freddy, LordJashin, Wanted.
Description: Returns MS in days, hours, minutes, seconds.
Date Created: August 7th, 2011. By Timer
Last Modified: March 3rd, 2013. By Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function MSToTime(MS: LongInt): string;
var
  tD, tH, tM, tS: Integer;
  TheTimeRInfo: array[0..3] of TStrArray;
  tDD, tHH, tMM, tSS: Boolean;
begin
  if (MS < 1000) then
    begin
      Result := IntToStr(MS) + ' milliseconds';
      Exit;
    end;
  ConvertTime(MS, tH, tM, tS);
  TheTimeRInfo[0] := [' day, ', ' days, ', ' day ', ' days '];
  TheTimeRInfo[1] := [' hour, ', ' hours, ', ' hour ', ' hours'];
  TheTimeRInfo[2] := [' minute, ', ' minutes, ', ' minute', ' minutes '];
  TheTimeRInfo[3] := [' second', ' seconds'];
  tD := tH div 24;
  tDD := tD > 0;
  tHH := tH > 0;
  tMM := tM > 0;
  tSS := tS > 0;
  if (tDD) then
    tH := tH - (tD * 24);
  if (tDD) then
    if (tD > 1) then
      if (tHH) or (tMM) or (tSS) then
        Result := Result + IntToStr(tD) + TheTimeRInfo[0][1]
      else
        Result := Result + IntToStr(tD) + TheTimeRInfo[0][3]
    else
      if (tHH) or (tMM) or (tSS) then
        Result := Result + IntToStr(tD) + TheTimeRInfo[0][0]
      else
        Result := Result + IntToStr(tD) + TheTimeRInfo[0][2];
  if (tHH) then
    if (tH > 1) then
      if (tMM) or (tSS) then
        Result := Result + IntToStr(tH) + TheTimeRInfo[1][1]
      else
        Result := Result + IntToStr(tH) + TheTimeRInfo[1][3]
    else
      if (tMM) or (tSS) then
        Result := Result + IntToStr(tH) + TheTimeRInfo[1][0]
      else
        Result := Result + IntToStr(tH) + TheTimeRInfo[1][2];
  if (tMM) then
    if (tM > 1) then
      if (tSS) then
        Result := Result + IntToStr(tM) + TheTimeRInfo[2][1]
      else
        Result := Result + IntToStr(tM) + TheTimeRInfo[2][3]
    else
      if (tSS) then
        Result := Result + IntToStr(tM) + TheTimeRInfo[2][0]
      else
        Result := Result + IntToStr(tM) + TheTimeRInfo[2][2];
  if (tSS) then
    if (tS > 1) then
      Result := Result + IntToStr(tS) + TheTimeRInfo[3][1]
    else
      Result := Result + IntToStr(tS) + TheTimeRInfo[3][0];
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function MSToTimeEx(MS: LongInt): string;
By: Wanted
Description: Returns MS in numbers Days:Hours:Minutes:Seconds:MiliSeconds
Date Created: October 8th, 2014. By Wanted
Last Modified:  October 19th, 2014. By Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function MSToTimeEx(MS: LongInt): string;
var
  SDays, SHours, SMinutes, SSeconds, SMSeconds: string;
  Days, Hours, Minutes, Seconds, Temp: LongInt;
begin
  Result := ''; 
  SDays := '';
  SHours := '';
  SMinutes := '';
  SSeconds := '';
  SMSeconds := '';  
  Days := (MS div 86400000); 
  Temp := (MS mod 86400000);
  if (Days > 0) then  
    SDays := IntToStr(Days) + ':';    
  Hours := (Temp div 3600000)                      
  Temp := (Temp mod 3600000);
  if (Hours > 0) then
    SHours := IntToStr(Hours) + ':';       
  Minutes := (Temp div 60000)                      
  Temp := (Temp mod 60000);
  if (Minutes > 0) then
    SMinutes := IntToStr(Minutes) + ':';    
  Seconds := (Temp div 1000)                      
  Temp := (Temp mod 1000);
  if (Seconds > 0) then
    SSeconds := IntToStr(Seconds) + ':';    
  if (SDays <> '') then
    case Length(SHours) of
      0: SHours := '00:';
      2: SHours := '0' + SHours; 
    end;      
  if (SHours <> '') then
    case Length(SMinutes) of
      0: SMinutes := '00:';
      2: SMinutes := '0' + SMinutes; 
    end;  
  if (SMinutes <> '') then
    case Length(SSeconds) of
      0: SSeconds := '00:';
      2: SSeconds := '0' + SSeconds; 
    end;      
  SMSeconds := IntToStr(Temp); 
  case Temp of
    0..9: SMSeconds := '00' + IntToStr(Temp);
    10..99: SMSeconds := '0' + IntToStr(Temp);   
  end;  
  Result := SDays + SHours + SMinutes + SSeconds + SMSeconds;
  if (SSeconds = '') then
    Result := Result + 'ms'            
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function STimeRunning: string;
By: Wanted
Description: Returns running time in days, hours, minutes, seconds.
Date Created: March 3rd, 2013. By Wanted
Last Modified: March 3rd, 2013. By Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function STimeRunning: string;
begin
  Result := MSToTime(GetTimeRunning);
end;