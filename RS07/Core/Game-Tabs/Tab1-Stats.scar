{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                              Official SCAR Include
                                  Runescape 07
                              Stats Tab 1 Routines
--------------------------------------------------------------------------------
 * function GetSkillBounds(Skill: Integer): TBox;
     By: Wanted
 * function GetSkillLevelEx(Skill: Integer; BottomNumber, CheckST: Boolean): Integer;
     By: Wanted
 * function GetSkillLevel(Skill: Integer; CheckST: Boolean): Integer;
     By: Wanted
 * function HoverSkill(Skill: Integer; CheckST: Boolean): Boolean;
     By: Wanted
 * function GetSkillInfo(Skill: Integer; CurrentCheck, NextCheck, RemainderCheck, CheckST: Boolean): TIntegerArray;
     By: Wanted
 * function GetXP(Skill: Integer; CheckST: Boolean): Integer;
     By: Wanted
 * function GetAllLevels(CheckST: Boolean): Boolean;
     By: Wanted
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetSkillBounds(Skill: Integer): TBox;
Contributors: Wanted
Description: Returns the box of the skill on the stats tab.
Date Created: November 1st, 2011. By Wanted. RS2 Build 672.
Last Modified: October 27th, 2014. By Wanted. RS07 Build ???.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetSkillBounds(Skill: Integer): TBox;
begin
  Result := BoxFromGrid(Point(548, 206), Skill, 62, 31, 3, 31, 63);
  if (Skill = Skill_Total) then
    Result := Box(676, 429, 734, 455);
  if (Skill = Skill_Combat) then
    Result := Box(600, 230, 690, 244); 
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetSkillLevelEx(Skill: Integer; BottomNumber, CheckST: Boolean): Integer;
Contributors: Wanted
Description: Returns the number from the skill level in skill tabs.
Date Created: November 3rd, 2011. By Wanted. RS2 Build 674.
Last Modified: October 27th, 2014. By Wanted. RS07 Build ???.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetSkillLevelEx(Skill: Integer; BottomNumber, CheckST: Boolean): Integer;
var
  TPA: TPointArray;
  B, B2: TBox;
begin
  Result := 0;
  if (CheckST) then
    if (not (SetGameTab(Tab_Stats))) then
      Exit;
  B := GetSkillBounds(Skill);
  if (BottomNumber) then
    B2 := Box(B.X1 + 25, B.Y1 + 14, B.X2, B.Y2)
  else
    B2 := Box(B.X1 + 25, B.Y1, B.X2, B.Y1 + 13);
  FindColorExP(TPA, 65535, B2);
  B := TPABounds(TPA);
  Result := StrToIntDef(GetNumbers(GetTextAtEx(B.X1 - 1, B.Y1 - 1, 0, StatChars, False, False, 0, 2, 65535, 10, False, tr_Digits)), -1);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetSkillLevel(Skill: Integer; CheckST: Boolean): Integer;
Contributors: Wanted
Description: Returns the number from the skill level in skill tabs.
Date Created: November 3rd, 2011. By Wanted. RS2 Build 674.
Last Modified: March 30th, 2012. By Wanted. RS2 Build 710.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetSkillLevel(Skill: Integer; CheckST: Boolean): Integer;
begin
  Result := GetSkillLevelEx(Skill, True, CheckST);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function HoverSkill(Skill: Integer; CheckST: Boolean): Boolean;
Contributors: Wanted
Description: Hovers a skill. Put a triple digit/negative number for a random skill.
Date Created: November 3rd, 2011. By Wanted. RS2 Build 674.
Last Modified: March 30th, 2012. By Wanted. RS2 Build 710.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function HoverSkill(Skill: Integer; CheckST: Boolean): Boolean;
var
  B: TBox;
begin
  Result := False; 
  if (CheckST) then
    if (not (SetGameTab(Tab_Stats))) then
      Exit;
  if (not (InRange(Skill, 0, Skill_Count - 1))) then
    B := GetSkillBounds(Random(Skill_Count))
  else
    B := GetSkillBounds(Skill);
  MouseBox(B.X1, B.Y1, B.X2, B.Y2, MoveTo);
  Result := True;
  WaitRR(1000, 2000);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetSkillInfo(Skill: Integer; CurrentCheck, NextCheck, RemainderCheck, CheckST: Boolean): TIntegerArray;
Contributors: Wanted
Description: Returns the information by hovering the skill.
Date Created: November 3rd, 2011. By Wanted. RS2 Build 674.
Last Modified: October 27th, 2014. By Wanted. RS07 Build ???.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetSkillInfo(Skill: Integer; CurrentCheck, NextCheck, RemainderCheck, CheckST: Boolean): TIntegerArray;
var
  TBoA: TBooleanArray;
  TSA: TStringArray;
  TPA: TPointArray;
  I: Integer;
  P: TPoint;
  B: TBox;
begin
  Result := [-1, -1, -1];
  if (not (HoverSkill(Skill, CheckST))) then
    Exit;
  if (not (WaitFindColorTolEx(TPA, clSkillInfoBox, MIB, 0, 50, True, 100, 200, 1500, 2500))) then
    Exit;
  B := TPABounds(TPA);
  B.Y1 := B.Y1 - 1;
  TSA := ['XP', 'ext', 'aining'];
  TBoA := [CurrentCheck, NextCheck, RemainderCheck];
  for I := 0 to 2 do
    if (TBoA[I]) then
      if (IsTextInAreaEx(B.X1, B.Y1, B.X2, B.Y2, P.X, P.Y, TSA[I], 0, SmallChars, False, False, 0, 2, clBlack)) then
        Result[I] := StrToIntDef(GetNumbers(GetTextAtEx(P.X, P.Y, 0, SmallChars, False, False, 0, 2, clBlack, 50, False, tr_AllChars)), -1);
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetXP(Skill: Integer; CheckST: Boolean): Integer;
Contributors: Wanted
Description: Returns the information by hovering the skill.
Date Created: November 3rd, 2011. By Wanted. RS2 Build 674.
Last Modified: March 30th, 2012. By Wanted. RS2 Build 710.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetXP(Skill: Integer; CheckST: Boolean): Integer;
var
  TIA: TIntegerArray;
begin
  TIA := GetSkillInfo(Skill, True, False, False, CheckST);
  Result := TIA[0];
end;

{=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
function GetAllLevels(CheckST: Boolean): Boolean;
Contributors: Wanted
Description: Stores all levels from the stats tab to current player.
Date Created: November 3rd, 2011. By Wanted. RS2 Build 674.
Last Modified: March 30th, 2012. By Wanted. RS2 Build 710.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=}

function GetAllLevels(CheckST: Boolean): Boolean;
var
  I, H: Integer;
begin
  Result := False;
  if (CheckST) then
    if (not (SetGameTab(Tab_Stats))) then
      Exit
    else
      Result := True;    
  H := Skill_Count - 1;
  for I := 0 to H do
    Players[CurrentPlayer].Level[I] := GetSkillLevel(I, False);
end;